apply plugin: 'checkstyle'
def checkstyleVersion = '10.12.1'
dependencies {
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"
}

def checkstyleConfigDir = file("${rootDir}/gradle/checkstyle/")

checkstyle {
    configFile = new File(checkstyleConfigDir, "checkstyle.xml")
    configProperties.checkstyleConfigDir = checkstyleConfigDir
}

plugins.withType(GroovyBasePlugin) {
    sourceSets.all { sourceSet ->
        task "${sourceSet.getTaskName('checkstyle', 'groovy')}"(type: Checkstyle) {
            configFile = new File(checkstyleConfigDir, "checkstyle-groovy.xml")
            source sourceSet.allGroovy
            classpath = sourceSet.compileClasspath
            reports.xml.destination new File(checkstyle.reportsDir, "${sourceSet.name}-groovy.xml")
        }
    }
}

def checkstyleTasks = tasks.withType(Checkstyle)

task checkstyle(dependsOn: checkstyleTasks)

check.dependsOn checkstyleTasks

tasks.withType(Test) {
    mustRunAfter checkstyleTasks
}